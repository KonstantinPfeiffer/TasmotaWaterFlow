>D
blinkpin=2
blink=0
blinkt=4
blinkc=0
p:total=0
p:liter=0
highflow=450
midflow=420
lowflow=390
highflowTC=150
midflowTC=80
lowflowTC=30
resetb=0
fastblink=2
midblink=4
slowblink=8
delta=0
flow=0
tmp=0
cflow="none"
lastval=0

>F
if blinkc>blinkt
then
if blink==0 
then
blink=1
else
blink=0
endif
spinm(blinkpin blink) 
blinkc=0
endif
if cflow!="none"
then
blinkc=blinkc+1
endif

>B
total=pc[1]


>S
if resetb==1
then
resetb=0
total=0
liter=0
=>Counter1 0 
endif

delta=pc[1]-total
total=pc[1]

if delta>highflowTC 
then
cflow="high"
blinkt=fastblink
tmp=(delta/highflow)
endif

if delta>midflowTC
and delta<highflowTC
then
cflow="mid"
blinkt=midblink
tmp=(delta/midflow)
endif

if delta<midflowTC
then
cflow="low"
blinkt=slowblink
tmp=(delta/lowflow)
endif

if delta==0
then
cflow="none"
blinkt=100000
spinm(blinkpin 0) 
tmp=0
endif


liter=(liter+tmp)
flow=tmp*60
print total:%total% delta:%delta% liter:%liter% flowrate:%flow%
svars

=>publish stat/%topic%/Flow {"FlowRate":%flow%,"PulseRate":%delta%,"WaterFlow":"%cflow%","Total":%liter%,"Counter":%total%}


>W

Total{m}%liter% Liter
FlowRate{m}%flow% Liter/min
PulseRate{m}%delta% Pulse/sec
WaterFlow{m}%cflow% 

bu(resetb "Resetting" "Reset Total")
